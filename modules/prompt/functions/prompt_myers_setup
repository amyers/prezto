#
# Andrew's riff on the sorin theme; c.f. prompt_sorin_setup
#
# Author (sorta):
#   Andrew Myers <myers@pobox.com>
#
# Original Author:
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#

# Load dependencies.
#pmodload 'helper'

function prompt_myers_precmd {
  local stat=$?
  local signal
  _prompt_myers_stat=""
  if [[ $stat -ne 0 ]] ; then
    if [[ $stat -gt 128 ]] ; then
      signal="$(kill -l $[$stat - 128] 2>/dev/null)"
      [[ -n "$signal" ]] && signal=" ($signal)"
    fi
    _prompt_myers_stat="%F{red}✘ ${stat}${signal}%f "
  fi

  _prompt_myers_host=$(hostname | sed -E 's/\..*//')
  _prompt_myers_pwd=$(prompt-pwd)

  if (( $+functions[git-info] )); then
    git-info
    scm_info_string="${git_info[status]}"
    [[ -n "$scm_info_string" ]] && scm_info_string=" $scm_info_string"
  fi
}

function prompt_myers_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent sp subst)

  # Load required functions.
  autoload -Uz add-zsh-hook

  add-zsh-hook precmd prompt_myers_precmd

  # Set editor-info parameters.
  zstyle ':prezto:module:editor:info:completing' format '%B%F{yellow}...%f%b'
  zstyle ':prezto:module:editor:info:keymap:primary' format '%F{yellow}$%f'
  zstyle ':prezto:module:editor:info:keymap:alternate' format '%F{yellow}¢%f'

  # Set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:action' format '%F{7}:%f%%B%F{9}%s%f%%b'
  zstyle ':prezto:module:git:info:added' format '%%B%F{2}✚%f%%b '
  zstyle ':prezto:module:git:info:ahead' format '%%B%F{13}⬆%f%%b '
  zstyle ':prezto:module:git:info:behind' format '%%B%F{13}⬇%f%%b '
  zstyle ':prezto:module:git:info:branch' format '%%B%F{2}%b%f%%b '
  zstyle ':prezto:module:git:info:commit' format '%%B%F{3}%.7c%f%%b'
  zstyle ':prezto:module:git:info:deleted' format '%%B%F{1}✖%f%%b '
  zstyle ':prezto:module:git:info:modified' format '%%B%F{4}✱%f%%b '
  zstyle ':prezto:module:git:info:position' format '%%B%F{13}%p%f%%b '
  zstyle ':prezto:module:git:info:renamed' format '%%B%F{5}➜%f%%b '
  zstyle ':prezto:module:git:info:stashed' format '%%B%F{6}✭%f%%b '
  zstyle ':prezto:module:git:info:unmerged' format '%%B%F{3}═%f%%b '
  zstyle ':prezto:module:git:info:untracked' format '%%B%F{7}◼%f%%b '
  zstyle ':prezto:module:git:info:keys' format \
    'status' '%b%p%c%s%A%B%S%a%d%m%r%U%u'

  # Define prompts.
  PROMPT='${_prompt_myers_stat}%F{yellow}${_prompt_myers_host}%f %F{cyan}${_prompt_myers_pwd}%f${scm_info_string}%(!. %B%F{red}#%f%b.)${editor_info[keymap]} '
  SPROMPT='zsh: correct %F{red}%R%f to %F{green}%r%f [nyae]? '
}

prompt_myers_setup "$@"
